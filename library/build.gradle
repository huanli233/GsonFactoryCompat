apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.vanniktech.maven.publish'
apply from : '../common.gradle'

android {
    defaultConfig {
        minSdkVersion 12
    }

    namespace 'com.hjq.gson.factory'

    android.libraryVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            outputFileName = "${rootProject.name}-${android.defaultConfig.versionName}.aar"
        }
    }
}

dependencies {
    // Json 解析框架：https://github.com/google/gson
    // noinspection GradleDependency
    implementation 'com.google.code.gson:gson:2.9.1'
    // Kotlin 反射依赖：用于反射 Kotlin data class 类对象
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.5.10'
}

// 防止编码问题
tasks.withType(Javadoc).configureEach {
    // prevent Javadoc from failing gradle build：https://stackoverflow.com/questions/35435173/prevent-javadoc-from-failing-gradle-build
    failOnError false
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

mavenPublishing {
    coordinates("com.huanli233.gsonfactory", "gsonfactory", "1.0.1")
    pom {
        name = 'GsonFactory'
        description = 'Gson compat'
        url = 'https://github.com/huanli233/GsonFactoryCompat'

        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id = 'huanli233'
                name = 'huanli233'
            }
        }

        scm {
            connection = 'scm:git:github.com/huanli233/GsonFactoryCompat.git'
            developerConnection = 'scm:git:ssh://github.com/huanli233/GsonFactoryCompat.git'
            url = 'https://github.com/huanli233/GsonFactoryCompat/tree/master'
        }
    }
}